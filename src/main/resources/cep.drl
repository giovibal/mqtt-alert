import io.github.giovibal.PirEvent

global io.github.giovibal.CepManager engine;
global io.github.giovibal.SoundPlayer soundPlayer;

declare PirEvent
    @role(event)
    @timestamp(timestamp)
//    @expires( 5m )
end

declare window GateEvents
    PirEvent() over window:time( 1m1s )
end

rule "Debug"
    when
        $s : PirEvent()
    then
        engine.logEvent("--- " + $s);
end


rule "Missing Communication"
    when
        $evt : PirEvent()
        not( PirEvent(this != $evt, this after[0,1m30s] $evt) )
    then
        engine.logEvent("--- Mancata Comunicazione !! ");
        soundPlayer.playAlertSound();
end


rule "Intrusion Detected"
    when
        $evt : PirEvent(msg contains "Presenza rilevata")
    then
        engine.logEvent("--- Presenza Rilevata !!");
        soundPlayer.playWarningSound();
end



rule "Intrusion Detected 2"
    when
        //$evt : PirEvent(msg contains "Presenza rilevata") over window:length(2)
        //PirEvent(this != $evt, msg contains "Presenza rilevata", this after[0,2m] $evt) over window:length(2)
        accumulate (
            PirEvent(msg contains "Presenza rilevata") over window:time(2m) ;
            $cnt : count( 1 );
            $cnt > 1
        )
    then
        engine.logEvent("--- Presenza Rilevata Consecutivamente !! count: " + $cnt);
        soundPlayer.playAlertSound();
end


rule "Missing Communication 2"
    when
        accumulate (
            PirEvent(msg contains "Nessuna presenza") over window:time(2m30s) ;
            $cnt : count( 1 );
            $cnt < 2
        )
    then
        engine.logEvent("--- Mancata Comunicazione 2 !! ( pochi messaggi rilevati in 1m30s: " + $cnt);
                soundPlayer.playAlertSound();
end

