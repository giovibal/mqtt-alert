import io.github.giovibal.PirEvent

global io.github.giovibal.CepManager engine;
global io.github.giovibal.SoundPlayer soundPlayer;

declare PirEvent
    @role(event)
    @timestamp(timestamp)
    @expires( 2m )
end

declare window GateEvents
    PirEvent() over window:time( 1m1s )
end

rule "Debug"
    when
        $s : PirEvent()
    then
        engine.logEvent("--- " + $s);
end


rule "Missing Communication"
    when
        $evt : PirEvent()
        not( PirEvent(this != $evt, this after[0,1m5s] $evt) )
    then
        engine.logEvent("--- Mancata Comunicazione !! ");
        soundPlayer.playAlertSound();
end


rule "Intrusion Detected"
    when
        $evt : PirEvent(msg contains "Presenza rilevata")
    then
        engine.logEvent("--- Presenza Rilevata !!");
        soundPlayer.playWarningSound();
end



rule "Intrusion Detected 2"
    when
        $evt : PirEvent(msg contains "Presenza rilevata")
        PirEvent(this != $evt, msg contains "Presenza rilevata", this after[0,2m] $evt)

        // after
        accumulate (
            PirEvent(msg contains "Presenza rilevata", this after[0,2m] $evt );
            $cnt : count( 1 );
            $cnt > 3
        )
    then
        engine.logEvent("--- Presenza Rilevata Consecutivamente!!");
        soundPlayer.playAlertSound();
end

